plugins {
    id 'java-gradle-plugin'
    id 'groovy'
    id 'com.gradle.plugin-publish' version '0.12.0'
}

group 'com.github.jazzschmidt'
version '1.0.1'

repositories {
    mavenCentral()
}

gradlePlugin {
    plugins {
        propertiesTemplatePlugin {
            id = 'com.github.jazzschmidt.properties-template-plugin'
            displayName = 'Propeties Template Plugin'
            implementationClass = 'com.github.jazzschmidt.PropertiesTemplatePlugin'
            description = '''\
            Ensures that all required project properties are present and not versioned under Git
            to prevent accidental security leaks. Missing properties are listed in a human-readable
            manner.
            
            Since the build is usually extended over and over again in long-term projects, this
            plugin supports integrating new project properties with a fail-fast approach.
            '''.stripIndent()
        }
    }
}

pluginBundle {
    website = 'https://github.com/jazzschmidt/gradle-properties-template-plugin'
    vcsUrl = 'https://github.com/jazzschmidt/gradle-properties-template-plugin'
    tags = [
            'properties', 'project', 'template', 'plugin', 'setup', 'prepare',
            'ci', 'continuous', 'integration', 'secrets', 'default', 'values',
            'security', 'leak', 'fail-fast', 'git', 'gitignore'
    ]
}


dependencies {
    implementation gradleApi()

    testImplementation gradleTestKit()
    testImplementation "org.codehaus.groovy:groovy-all:2.5.8"
    testImplementation platform("org.spockframework:spock-bom:2.0-M1-groovy-2.5")
    testImplementation "org.spockframework:spock-core"
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
}
